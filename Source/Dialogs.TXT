.Dialogs

. display a message dialog
. dlgfield = [Prompt Text]
. buttonText1 = OK
msgdialog LROUTINE dialogText,buttonText1
dialogText	CHAR @
buttonText1 	CHAR @

dialogRes		RESOURCE
dialogQue		QUEUE
dialogMsg		RECORD
name      	CHAR 8
func      	CHAR 4
item      	NUM  5
horz 		NUM  5
vert			NUM  5
data	  		CHAR 40
	     	RECORDEND

dialogDef		CHAR 300

	CLEAR dialogDef
	PACK dialogDef,"DIALOG=D1, SIZE=200:100":
			  ",H=01,V=20,CTEXT=",dialogText,":200":
			  ",H=60,V=60,PUSHBUTTON=99:",buttonText1,":80:24"
	RESET dialogDef

	PREP dialogRes,dialogDef
	EMPTY windowQue
 	LINK dialogRes,dialogQue
	SHOW dialogRes,windowDev

	LOOP
		WAIT dialogQue
		GET dialogQue;dialogMsg
		SWITCH dialogMsg.func
			CASE "CLOS"
				BREAK
			CASE "PUSH"
				SWITCH dialogMsg.item
					CASE 99
						BREAK
				ENDSWITCH
		ENDSWITCH
	REPEAT

	HIDE dialogRes
	UNLINK dialogRes
	CLOSE dialogRes

	RETURN
	ENDROUTINE

. display an OK and Cancel dialog
. dlgfield = [Prompt Text]
. buttonText1 = OK
. buttonText2 = Cancel
okdialog LROUTINE dialogText,buttonText1,buttonText2
dialogText	CHAR @
buttonText1 	CHAR @
buttonText2 	CHAR @

dialogRes		RESOURCE
dialogQue		QUEUE
dialogMsg		RECORD
name      	CHAR 8
func      	CHAR 4
item      	NUM  5
horz 		NUM  5
vert			NUM  5
data	  		CHAR 40
	     	RECORDEND

dialogDef		CHAR 300

	CLEAR dialogDef
	PACK dialogDef,"DIALOG=D2, NOCLOSE, SIZE=200:10":
			  ",H=001,V=20,CTEXT=",dialogText,":200":
			  ",H=024,V=60,PUSHBUTTON=98:",buttonText1,":64:24":
			  ",H=112,V=60, PUSHBUTTON=99:",buttonText2,":64:24"
	RESET dialogDef

	PREP dialogRes,dialogDef
	EMPTY windowQue
 	LINK dialogRes,dialogQue
	SHOW dialogRes,windowDev

	LOOP
		WAIT dialogQue
		GET dialogQue;dialogMsg
		SWITCH dialogMsg.func
			CASE "CLOS"
				BREAK
			CASE "PUSH"
				SWITCH dialogMsg.item
					CASE 98
						SETFLAG NOT OVER
						BREAK
					CASE 99
						SETFLAG OVER
						BREAK
				ENDSWITCH
		ENDSWITCH
	REPEAT

	HIDE dialogRes
	UNLINK dialogRes
	CLOSE dialogRes

	RETURN
	ENDROUTINE

. display a dialog the inputs a field
. dlgfield = [Prompt Text]
. dlgfield = [Capture Field]
. buttonText1 = OK
. buttonText2 = Cancel
fielddialog LROUTINE dialogText,dlgfield,buttonText1,buttonText2
dialogText	CHAR @
buttonText1 	CHAR @
buttonText2 	CHAR @
dlgfield		CHAR @

dialogRes		RESOURCE
dialogQue		QUEUE
dialogMsg		RECORD
name      	CHAR 8
func      	CHAR 4
item      	NUM  5
horz 		NUM  5
vert			NUM  5
data	  		CHAR 40
	     	RECORDEND

dialogDef		CHAR 300

	CLEAR dialogDef
	PACK dialogDef,"DIALOG=D3, SIZE=300:100":
			  ",H=010,V=20,TEXT=",dialogText:
			  ",H=150,V=20, EDIT=91::140":
			  ",H=024,V=60,PUSHBUTTON=98:",buttonText1,":64:24":
			  ",H=112,V=60,PUSHBUTTON=99:",buttonText2,":64:24"

	RESET dialogDef
	PREP dialogRes, dialogDef
	EMPTY windowQue
 	LINK dialogRes,dialogQue
	SHOW dialogRes,windowDev

	LOOP
		WAIT dialogQue
		GET dialogQue;dialogMsg
		SWITCH dialogMsg.func
			CASE "CLOS"
				BREAK
			CASE "PUSH"
				SWITCH dialogMsg.item
					CASE 98
						QUERY dialogRes,"STATUS91";dlgfield
						SETFLAG NOT OVER
						BREAK
					CASE 99
						SETFLAG OVER
						BREAK
				ENDSWITCH
		ENDSWITCH
	REPEAT

	HIDE dialogRes
	UNLINK dialogRes
	CLOSE dialogRes

	RETURN
	ENDROUTINE
